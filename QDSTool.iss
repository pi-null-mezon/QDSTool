; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "QDSTool"
#define MyAppVersion "1.0.0"
#define MyAppPublisher "pi-mezon.ru"
#define MyAppExeName "QDSTool.exe"
#define MyAppURL "https://github.com/pi-null-mezon/QDSTool.git"
  
#define Opencv_version 450
#define Opencv_path "C:\Programming\3rdParties\opencv450\build\x86\mingw\bin"
#define Qt "C:\Qt\5.14.2\mingw73_32\bin"
#define Q_T_V "5_14_2"
#define ARCH "x86"

#define DCMTK_PATH "C:\Programming\3rdParties\dcmtk-3.6.6\install\bin"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{D228DE12-8901-4AD6-8719-5353DBF30F56}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
;AppPublisherURL={#MyAppURL}
;AppSupportURL={#MyAppURL}
;AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppPublisher}\{#MyAppName}
DisableWelcomePage=yes
DisableDirPage=yes
DisableProgramGroupPage=yes
OutputDir=C:\Programming\Releases
OutputBaseFilename=SETUP_{#MyAppName}_v{#MyAppVersion}_{#ARCH}
Compression=lzma
SolidCompression=yes
#ifdef AMD64
; "ArchitecturesAllowed=x64" specifies that Setup cannot run on
; anything but x64.
ArchitecturesAllowed=x64
; "ArchitecturesInstallIn64BitMode=x64" requests that the install be
; done in "64-bit mode" on x64, meaning it should use the native
; 64-bit Program Files directory and the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64
#endif
; Writing to {commonstartup} requires administrative privileges
PrivilegesRequired=admin

[Languages]
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "English"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "C:\Programming\build\build-{#MyAppName}-Desktop_Qt_{#Q_T_V}_MinGW_32_bit-Release\release\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
; OpenBLAS
#ifndef AMD64
Source: "C:\Program Files (x86)\OpenBLAS\bin\*.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Qt\Tools\mingw730_32\bin\libgfortran-4.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Qt\Tools\mingw730_32\bin\libquadmath-0.dll"; DestDir: "{app}"; Flags: ignoreversion
#else
Source: "C:\Program Files\OpenBLAS\bin\*.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Qt\Tools\mingw730_64\bin\libgfortran-4.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Qt\Tools\mingw730_64\bin\libquadmath-0.dll"; DestDir: "{app}"; Flags: ignoreversion
#endif
; Qt parts
Source: "{#Qt}\libgcc_s_dw2-1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\libstdc++-6.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\libwinpthread-1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\libEGL.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\libGLESv2.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\d3dcompiler_47.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\opengl32sw.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\Qt5Core.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Qt}\Qt5Qml.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Qt}\Qt5Quick.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Qt}\Qt5QuickControls2.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Qt}\Qt5QuickTemplates2.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\Qt5Concurrent.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\Qt5Gui.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\Qt5Network.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\Qt5OpenGL.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\Qt5Widgets.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Qt}\Qt5Multimedia.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Qt}\Qt5MultimediaQuick.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\Qt5Svg.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Qt}\Qt5QmlModels.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Qt}\Qt5QmlWorkerScript.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Qt}\..\plugins\platforms\qwindows.dll"; DestDir: "{app}\platforms"; Flags: ignoreversion
;Source: "{#Qt}\..\plugins\mediaservice\*.dll"; Excludes: "*d.dll"; DestDir: "{app}\mediaservice"; Flags: ignoreversion
;Source: "{#Qt}\..\plugins\audio\*.dll"; Excludes: "*d.dll"; DestDir: "{app}\audio"; Flags: ignoreversion
;Source: "{#Qt}\..\plugins\qmltooling\*"; Excludes: "*d.dll"; DestDir: "{app}\qmltooling"; Flags: ignoreversion
Source: "{#Qt}\..\plugins\imageformats\*"; Excludes: "*d.dll,qgif*,qjpeg*,qwebp*"; DestDir: "{app}\imageformats"; Flags: ignoreversion
; Opencv
Source: "{#Opencv_path}\libopencv_core{#Opencv_version}.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Opencv_path}\libopencv_calib3d{#Opencv_version}.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Opencv_path}\libopencv_features2d{#Opencv_version}.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Opencv_path}\libopencv_flann{#Opencv_version}.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Opencv_path}\libopencv_imgproc{#Opencv_version}.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Opencv_path}\libopencv_imgcodecs{#Opencv_version}.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Opencv_path}\libopencv_videoio{#Opencv_version}.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "{#Opencv_path}\libopencv_dnn{#Opencv_version}.dll"; DestDir: "{app}"; Flags: ignoreversion
; DCMTK
Source: "{#DCMTK_PATH}\libdcmdata.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#DCMTK_PATH}\libdcmfg.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#DCMTK_PATH}\libdcmimgle.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#DCMTK_PATH}\liboflog.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#DCMTK_PATH}\libofstd.dll"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
; Add application to autostart
;Name: "{commonstartup}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
//-------------------------------------------------------------------------------------
// Delete application's previous version if it had been installed before
//-------------------------------------------------------------------------------------
function GetUninstallString: string;
var
  sUnInstPath: string;
  sUnInstallString: String;
begin
  Result := '';
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{{D228DE12-8901-4AD6-8719-5353DBF30F56}_is1'); //Your App GUID/ID
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;

function IsUpgrade: Boolean;
begin
  Result := (GetUninstallString() <> '');
end;

function InitializeSetup: Boolean;
var
  V: Integer;
  iResultCode: Integer;
  sUnInstallString: string;
  custommsg: string;
begin
  Result := True; // in case when no previous version is found
  if RegValueExists(HKEY_LOCAL_MACHINE,'Software\Microsoft\Windows\CurrentVersion\Uninstall\{D228DE12-8901-4AD6-8719-5353DBF30F56}_is1', 'UninstallString') then  //Your App GUID/ID
  begin
    custommsg := 'Обнаружена старая версия программы. Вы должны удалить её перед установкой новой версии. Удалить сейчас?';
    if ActiveLanguage = 'English' then
      custommsg := 'An old version of the program has been detected. You should remove it before installing the new version. Delete now?';
     
    V := MsgBox(custommsg, mbInformation, MB_YESNO); //Custom Message if App installed
    if V = IDYES then
    begin
      sUnInstallString := GetUninstallString();
      sUnInstallString :=  RemoveQuotes(sUnInstallString);
      Exec(ExpandConstant(sUnInstallString), '', '', SW_SHOW, ewWaitUntilTerminated, iResultCode);
      Result := True; //if you want to proceed after uninstall
      //Exit; //if you want to quit after uninstall
    end
    else
      Result := False; //when older version present and not uninstalled
  end;
end;
//-------------------------------------------------------------------------------------